PRO sfhplot_deco,halofile, binsize,outplot  = outplot,yrange = yrange

;; This code creates  a star formation history
;; in units of Msol/yr for an n-body shop simulation.  Code will
;; generate disk and bulge SFH plot and 
;; output a file with information on star 
;; formation in all components.
;;
;; Code is easy to modify if plots of SFH in other components is wanted
;;
;; The code assumes that total disk is thin + thick + pseudobulge
;; but this is easily modified.

if (n_params() lt 2) then begin
 print, 'Usage: sfhplot, halofile, time bin (Gyr),[outplot = outplot]'
 print, ' '
 print, 'Script expects to find halo file, parameter file and .cmp file'
 print, 'in directory in which it is run.  Check naming conventions.'
 print, ' '
 print, 'Script assumes LCDM cosmology for time units (38.768); '
 stop
endif
loadct,39
formatplot,outplot = outplot


;; Read in halo file
rtipsy,halofile,h,g,d,s

;; Read in the .cmp generated by kinematic decomposition
cmpfile = strcompress(halofile + '.decomp')
cmp = read_lon_array(cmpfile)


;- Make thin/thick disk, halo, bulge, pseudobule
;  arrays from cmp file (including all particles)
;- Tally up number of gas and dark particles
;- Remove non-stellar particles from indexing
;  of cmp arrays using this number
;
; Warning: this assumes all stellar particles are 
; at the bottom of the cmp array!!
thindisk = where(cmp eq 1)
halo = where(cmp eq 2)
bulge = where(cmp eq 3)
thickdisk = where(cmp eq 4)
pseudobulge = where(cmp eq 5)
;; For our purposes, combine thin,thick and pseudobulge into 
;; one "disk" parameter
disk = where((cmp eq 1) or (cmp eq 4) or (cmp eq 5))
galaxy = where((cmp eq 1) or (cmp eq 2) or (cmp eq 3) or (cmp eq 4) or (cmp eq 5))

print, "Num thin disk stars is ",n_elements(thindisk)
print, "Num thick disk stars is ",n_elements(thickdisk)
print, "Num pseudobulge stars is ",n_elements(pseudobulge)
print, "Tot num disk stars is ",n_elements(disk)

totdg = h.ngas+h.ndark

thindisk = thindisk - totdg
halo = halo - totdg
bulge = bulge - totdg
thickdisk = thickdisk - totdg
pseudobulge = pseudobulge - totdg
disk = disk - totdg
galaxy = galaxy-totdg

;; Read in parameter file to get initial stellar mass
;; and stellar mass unit
n = strlen(halofile)
m = n - 6
halopart = strmid(halofile,0,m)
paramfile=strcompress(halopart+'.param')

;readcol,paramfile,format='a,a,f,',name,dummy2,params
;wg1 = where(name eq 'dInitStarMass')
;initStarMass = params[wg1]
;wg2 = where(name eq 'dMsolUnit')
;MsolUnit = params[wg2]
;print, string(name[wg1])+'= '+string(params[wg1])
;print, string(name[wg2])+'= '+string(params[wg2])
units = tipsyunits(paramfile)

;; To set SFH in physical units of Msol/yr
phys = units.istarmass*units.massunit/(binsize*1.0e+09)


;; The time unit in the following section (38.768) assumes an
;; age of 13.73 Gyr for the Universe, i.e., H_0=73, omega_0=0.24, 
;; and Lambda=0.76, and a maximum tform of 0.35415 for star particles.
;; You may want to double-check this applies to your simulations.
mintime=min(s.tform)*units.timeunit
maxtime=max(s.tform)*units.timeunit

halohist=histogram(((s[halo].tform)*units.timeunit),locations=xhist,bin=binsize,max=maxtime, min=mintime)
thinhist=histogram(((s[thindisk].tform)*units.timeunit),locations=xhist2, bin=binsize,max=maxtime, min=mintime)
thickhist=histogram(((s[thickdisk].tform)*units.timeunit),bin=binsize,max=maxtime, min=mintime)
pseudohist=histogram(((s[pseudobulge].tform)*units.timeunit),locations=xhist3,bin=binsize,max=maxtime, min=mintime)
diskhist=histogram(((s[disk].tform)*units.timeunit),bin=binsize,max=maxtime, min=mintime)
bulgehist=histogram(((s[bulge].tform)*units.timeunit),bin=binsize,max=maxtime, min=mintime)
tothist=histogram(((s[galaxy].tform)*units.timeunit),bin=binsize,max=maxtime, min=mintime)

;; This plots disk and bulge SFH but pick your favorite galaxy components

IF KEYWORD_SET(outplot) THEN BEGIN
    device,filename=outplot + '_GalSFH.eps',/encapsulate,/color
ENDIF
IF NOT KEYWORD_SET(yrange) THEN yrange = [0,MAX(diskhist*phys)]
plot,xhist/1e9,bulgehist*phys,psym=10,/nodata,xtit='Age of Universe (Gyr)',$
  ytit='M!Lsol!N/yr',xrange=[0,14],yrange = yrange
oplot,xhist/1e9,halohist*phys,psym=10,color=20
oplot,xhist/1e9,bulgehist*phys,psym=10,color=75
oplot,xhist/1e9,pseudohist*phys,psym=10,color=100
oplot,xhist/1e9,thickhist*phys,psym=10,color=150
oplot,xhist/1e9,thinhist*phys,psym=10,color=245
;oplot,xhist/1e9,diskhist*phys,psym=10,color=250
;legend, ['Disk','Bulge'],textcolors=[250,75],/top,/right
legend, ['Halo','Bulge','Pseudo Bulge','Thick Disk','Thin Disk'],colors=[20,75,100,150,245],linestyle = [0,0,0,0,0],/top,/right

IF KEYWORD_SET(outplot) THEN device,/close

;; Print out histogram values to a file
openw,lun1,'sfhist.dat',/get_lun
printf, lun1, 'Time (Gyr), Bulge, Halo, Pseudobulge, Thin Disk, Thick Disk, Disk (Tot), Total SFR' 
n = n_elements(xhist)
for i=0,n-1 do begin
    
    ;totSFR = bulgehist[i]+halohist[i]+thinhist[i]+thickhist[i]+pseudohist[i]
   
    printf, lun1, string(xhist[i]) + string(bulgehist[i]*phys) + string(halohist[i]*phys) $
      + string(pseudohist[i]*phys) + string(thinhist[i]*phys) + string(thickhist[i]*phys)  $
      + string(diskhist[i]*phys) + string(tothist[i]*phys)
    
endfor

free_lun, lun1
close,/all

RETURN
;STOP
END

